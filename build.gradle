buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.7"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

jar {
    baseName = 'appstarter'
    version = '0.0.10-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-freemarker")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.apache.httpcomponents:httpclient:4.5")
    compile("org.apache.commons:commons-lang3:3.4")
    compile("javax.inject:javax.inject:1")
    compile("com.google.guava:guava:12.0")
    compile("org.flywaydb:flyway-core")
    compile("org.jadira.usertype:usertype.extended:4.0.0.GA")
    compile("com.sun.mail:javax.mail")
    optional("org.springframework.boot:spring-boot-configuration-processor")
    runtime("org.postgresql:postgresql:9.4-1203-jdbc42")
    testCompile("org.seleniumhq.selenium:selenium-java:2.48.2")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("net.sourceforge.htmlunit:htmlunit:2.18")
    testCompile("org.exparity:hamcrest-date:2.0.1")
    testCompile("com.icegreen:greenmail:1.4.1")
    testCompile("org.springframework.security:spring-security-test:4.0.3.RELEASE")
}

// from http://docs.spring.io/spring-boot/docs/1.3.0.RELEASE/reference/html/configuration-metadata.html#configuration-metadata-annotation-processor
compileJava.dependsOn(processResources)

springBoot {
    mainClass = "com.bright.appstarter.AppStarterApplication"
}

bootRun {
    addResources = true
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main")
        downloadJavadoc = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
